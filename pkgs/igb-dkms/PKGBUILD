_pkgbase=igb
pkgname=igb-dkms
pkgver=5.8.5
pkgrel=2
pkgdesc="Intel igb Ethernet adapter driver (DKMS version)"
license=('GPL')
arch=('i686' 'x86_64')
depends=('dkms')
conflicts=("${_pkgbase}")
install=${pkgname}.install
url='http://sourceforge.net/projects/e1000/'
source=("http://downloads.sourceforge.net/project/e1000/${_pkgbase}%20stable/${pkgver}/${_pkgbase}-${pkgver}.tar.gz"
        'dkms.conf'
        'igb-dkms.install')
sha384sums=('ce793d9f53269b8475764d0075ddcbd9149b9dc02ed8232471a4ef7ee445ef4d121fd2a249c7a50e285a415c36144656'
            '1148f38925218390389b3f6cc01c4444e91f3354220093848c8bc262a078ac09091d12343da58a36806527516309439a'
            'b67c0f9b934c7ff480b0927fb51c52c5385c1040c1badd45506f9b0eb445504f000c20dd87e235b7e60dc25302e141ce')

package() {
  install -Dm644 "${srcdir}/dkms.conf" "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/dkms.conf"

  # Set name and version
  sed -e "s/@_PKGBASE@/${_pkgbase}/" \
      -e "s/@PKGVER@/${pkgver}/" \
      -i "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.conf

  cp -r ${srcdir}/${_pkgbase}-${pkgver}/src/* "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/
}
